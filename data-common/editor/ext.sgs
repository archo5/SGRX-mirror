
global ED_IMGUI = {};
global ED_ILOAD = {};
global ED_ISAVE = {};
global ED_ILCSV = class({}, ED_ISAVE);

// link to loaded/saved object data
global ED_SDATA = null;

function ED_ILOAD.EditBool( lbl, obj, prop )
{
	obj.(prop) = tobool( ED_SDATA[ prop ] );
}
function ED_ISAVE.EditBool( lbl, obj, prop )
{
	ED_SDATA[ prop ] = tobool( obj.(prop) );
}
// default ED_ILCSV

function ED_ILOAD.EditInt( lbl, obj, prop, vmin, vmax )
{
	obj.(prop) = toint( ED_SDATA[ prop ] );
}
function ED_ISAVE.EditInt( lbl, obj, prop, vmin, vmax )
{
	ED_SDATA[ prop ] = toint( obj.(prop) );
}
// default ED_ILCSV

function ED_ILOAD.EditFloat( lbl, obj, prop, vmin, vmax, prec )
{
	obj.(prop) = toreal( ED_SDATA[ prop ] );
}
function ED_ISAVE.EditFloat( lbl, obj, prop, vmin, vmax, prec )
{
	ED_SDATA[ prop ] = toreal( obj.(prop) );
}
// default ED_ILCSV

function ED_ILOAD.EditVec3( lbl, obj, prop, vmin, vmax, prec )
{
	d = ED_SDATA[ prop ];
	obj.(prop) = vec3( d.x, d.y, d.z );
}
function ED_ISAVE.EditVec3( lbl, obj, prop, vmin, vmax, prec )
{
	v = obj.(prop);
	ED_SDATA[ prop ] = { x = v.x, y = v.y, z = v.z };
}
function ED_ILCSV.EditVec3( lbl, obj, prop, vmin, vmax, prec )
{
	ED_SDATA[ prop ] = vec3( obj.(prop) );
}

function ED_ILOAD.EditMask( lbl, obj, prop, count )
{
	obj.(prop) = toint( ED_SDATA[ prop ] );
}
function ED_ISAVE.EditMask( lbl, obj, prop, count )
{
	ED_SDATA[ prop ] = toint( obj.(prop) );
}
// default ED_ILCSV

function ED_ILOAD.EditColorRGBLDR( lbl, obj, prop )
{
	d = ED_SDATA[ prop ];
	obj.(prop) = vec3( d.x, d.y, d.z );
}
function ED_ISAVE.EditColorRGBLDR( lbl, obj, prop )
{
	v = obj.(prop);
	ED_SDATA[ prop ] = { x = v.x, y = v.y, z = v.z };
}
function ED_ILCSV.EditColorRGBLDR( lbl, obj, prop )
{
	ED_SDATA[ prop ] = vec3( obj.(prop) );
}

function ED_ILOAD.EditXFMat4( lbl, obj, prop )
{
	a = ED_SDATA[ prop ];
	m = mat4();
	if( a != null )
	{
		for( i = 0; i < 16; ++i )
			m[ i ] = a[ i ];
	}
	obj.(prop) = m;
}
function ED_ISAVE.EditXFMat4( lbl, obj, prop )
{
	m = obj.(prop);
	a = [].resize( 16 );
	for( i = 0; i < 16; ++i )
		a[ i ] = m[ i ];
	ED_SDATA[ prop ] = a;
}
function ED_ILCSV.EditXFMat4( lbl, obj, prop )
{
	ED_SDATA[ prop ] = mat4( obj.(prop) );
}

function ED_ILOAD.PickMesh( lbl, obj, prop, caption ){ obj.(prop) = tostring( ED_SDATA[ prop ] ); }
function ED_ISAVE.PickMesh( lbl, obj, prop, caption ){ ED_SDATA[ prop ] = tostring( obj.(prop) ); }
// default ED_ILCSV

function ED_ILOAD.PickTexture( lbl, obj, prop, caption ){ obj.(prop) = tostring( ED_SDATA[ prop ] ); }
function ED_ISAVE.PickTexture( lbl, obj, prop, caption ){ ED_SDATA[ prop ] = tostring( obj.(prop) ); }
// default ED_ILCSV

function ED_ILOAD.PickPartSys( lbl, obj, prop, caption ){ obj.(prop) = tostring( ED_SDATA[ prop ] ); }
function ED_ISAVE.PickPartSys( lbl, obj, prop, caption ){ ED_SDATA[ prop ] = tostring( obj.(prop) ); }
// default ED_ILCSV

function ED_ILOAD.PickSound( lbl, obj, prop, caption ){ obj.(prop) = tostring( ED_SDATA[ prop ] ); }
function ED_ISAVE.PickSound( lbl, obj, prop, caption ){ ED_SDATA[ prop ] = tostring( obj.(prop) ); }
// default ED_ILCSV

function ED_ILOAD.PickLocalRsrc( lbl, obj, prop, gobj )
{
	obj.(prop) = null;
	// TODO delayed mapping
}
function ED_ISAVE.PickLocalRsrc( lbl, obj, prop, gobj )
{
	ED_SDATA[ prop ] = tostring( @obj.(prop).__guid ?? "" );
}
// default ED_ILCSV

function ED_ILOAD.ComboNT( lbl, obj, prop, zssl, start )
{
	obj.(prop) = toint( ED_SDATA[ prop ] );
}
function ED_ISAVE.ComboNT( lbl, obj, prop, zssl, start )
{
	ED_SDATA[ prop ] = toint( obj.(prop) );
}
// default ED_ILCSV

function ED_ILOAD.Button( lbl ){}
function ED_ISAVE.Button( lbl ){}
// default ED_ILCSV

