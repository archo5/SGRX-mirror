
include "../levels/ui_common";

_G.
{
	tex_background = TEXTURE("ui/bg1.png"),
	tex_button_core_n = TEXTURE("ui/button_core.png"),
	tex_button_core_h = TEXTURE("ui/button_core_h.png"),
	tex_button_core_c = TEXTURE("ui/button_core_c.png"),
	tex_button_core_f = TEXTURE("ui/button_core_f.png"),
};

global scr_background = ROOT.CreateScreen( GUI_ScrMode_Crop, 512, 512 ).{ z = -1 };
t = 0;
scr_background.shaders.push(function( ctrl, dt ) use( t )
{
	t += dt;
	ctrl.DTex( tex_background );
	ctrl.DQuadExt( 0, 0, 512, 512, t * -0.02 );
	process_threads();
});

global scr_mainmenu = ROOT.CreateScreen( GUI_ScrMode_Fit, 105, 100 );
scr_mainmenu.shaders.push(function( ctrl )
{
	ctrl.DCol( 0.5, 0.5 );
	ctrl.DQuad( 5, 5, ctrl.width - 5, ctrl.height - 5 );
	ctrl.DCol( 0.1, 0.8 );
	ctrl.DFont( "core", 4 );
	ctrl.DText( "Main menu", 99, 6, HALIGN_RIGHT, VALIGN_TOP );
});

global scr_button1 = scr_mainmenu!CreateButton( 10, 10, 40, 5, "Click me" );
scr_button1.AddCallback( "onclick", function()
{
	println( "clicked!" );
	thread YesNoQuestion( "Do you want to do something?" );
});

global scr_button2 = scr_mainmenu!CreateButton( 10, 20, 40, 5, "Click me 2" );
scr_button2.AddCallback( "onclick", function()
{
	println( "clicked 2!" );
	thread YesNoQuestion( "Do you want to do something?" );
});

global scr_switch1 = scr_mainmenu!CreateSwitchButton(
	10, 30, 40, 5, 5, ["One", "Two", "Three"], "all/systems/test" );
ev_change."all/systems/test" = function(k){ println("\\ all ("$values[k]$")"); };
ev_change."all/systems" = function(k){ println("\\ systems ("$values[k]$")"); };
ev_change."all" = function(k){ println("\\ test ("$values[k]$")"); };

function proctf( val ){ return round( val ) $ "%"; }
global scr_slide1a = scr_mainmenu!CreateSlider( 10, 40, 40, 5, 0, "all/sld1a", 10 );
global scr_slide1b = scr_mainmenu!CreateSlider( 55, 40, 40, 5, 4, "all/sld1b", 10 );
global scr_slide2a = scr_mainmenu!CreateSlider( 10, 50, 40, 5, 0, "all/sld2a", 100.0, proctf );
global scr_slide2b = scr_mainmenu!CreateSlider( 55, 50, 40, 5, 4, "all/sld2b", 100.0, proctf );


global scr_optmenu = ROOT.CreateScreen( GUI_ScrMode_Fit, 105, 100, 0, 0, 2000 );
scr_optmenu.shaders.push(function( ctrl )
{
	ctrl.DCol( 0.4, 0.5 );
	ctrl.DQuad( 5, 5, ctrl.width - 5, ctrl.height - 5 );
	ctrl.DFont( "core", 4 );
	ctrl.DCol( 0.1, 0.8 );
	ctrl.DText( "Options", 99, 6, HALIGN_RIGHT, VALIGN_TOP );
});
scr_optmenu.enabled = false;


global scr_btn_main2options = scr_mainmenu!CreateButton( 95, 90, 5, 5, ">" );
scr_btn_main2options.AddCallback( "onclick", function()
{
	thread (function()
	{
		scr_optmenu.enabled = true;
		a1 = subthread animate( scr_mainmenu, "x", -2000, 0.2 );
		a2 = subthread animate( scr_optmenu, "x", 0, 0.2 );
		sync( a1, a2 );
		scr_mainmenu.enabled = false;
	})();
});

global scr_btn_options2main = scr_optmenu!CreateButton( 95, 90, 5, 5, "<" );
scr_btn_options2main.AddCallback( "onclick", function()
{
	thread (function()
	{
		scr_mainmenu.enabled = true;
		a1 = subthread animate( scr_optmenu, "x", 2000, 0.2 );
		a2 = subthread animate( scr_mainmenu, "x", 0, 0.2 );
		sync( a1, a2 );
		scr_optmenu.enabled = false;
	})();
});


DoCommand( "r_videomode 0 600 600 60 0 1 0 0" );


foreach( name : [
	"r_display",
	"r_width",
	"r_height",
	"r_refresh_rate",
	"r_fullscreen",
	"r_vsync",
	] )
{
	print( name, ": " );
	printvar( GetCVar( name ) );
}

